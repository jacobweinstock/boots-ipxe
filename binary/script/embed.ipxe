#!ipxe

set user-class Tinkerbell
echo Welcome to Neverland!

# Allow the operator to drop to a shell
prompt --key 0x02 --timeout 2000 Press Ctrl-B for the iPXE command line... && shell ||

# Random string to enable binary patching
#a8b7e61f1075c37a793f2f92cee89f7bba00c4a8d7842ce3d40b5889032d88811018d6dd793b4889a09b5c78638d1745e239764a22c4440d8697cfa9aebcdcc4
#ddd16a4fc4926ecefdfb6941e33c44ed3647133638f5e84021ea44d3152e7f97ddca37bae3474fb3afdbec7e26d4a5c444801f93140440e3-a4e0-64885578b7fd

# This is possible because the DHCP options from the original vendor PXE DHCP request
# are available to chainloaded iPXE binaries. See https://github.com/ipxe/ipxe/issues/188
set vlan-id ${43.116:string}
isset ${vlan-id} && goto boot-with-vlan ||

:iterate-interfaces
set idx:int32 0
:interfaces-loop
isset ${net${idx}/ip} && goto interfaces-loop-done || iseq ${idx} 50 && goto autoboot || inc idx && goto interfaces-loop

:interfaces-loop-done
echo Booting from net${idx}...
autoboot net${idx}

:autoboot
autoboot

:boot-with-vlan
set idx:int32 0
# Find the interface that is configured with an IP, this will be the iPXE auto created vlan interface.
:loop isset ${net${idx}-${vlan-id}/ip} && goto loop_done ||
  iseq ${idx} 50 && goto error ||
  inc idx && goto loop

:loop_done
echo Booting from net${idx}-${vlan-id}...
autoboot net${idx}-${vlan-id}

:error
echo Failed to find the vlan interface
shell
